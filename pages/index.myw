##  Extra Notations

The core MyWord notation for inline elements is the label and bracket notation. This can be used to represent any HTML element, and the content of the brackets can use any other syntax that is defined in a lingo. The core notation is easy to read, and it is very flexible and extendable.  However, there are some other lightweight markup notations, such as the Markdown notations, that have become very popular, and they are even easier to read.

For this reason MyWord enables a variety of extra notations that can be defined in much the same way that labels are defined for the core notation. This allow most of the familiar Markdown notations to be emulated. The extra notations include symbols, quotes, and double bracket notations.

##  Symbols

HTML allows any Unicode symbol to be expressed as an entity. For example: `&`rarr; for a &rarr; symbol.

Some symbols can be directly represented with common characters, for example `[->] to represent the &rarr; arrow.

To enable symbol notations MyWord allows a definition pattern inside parentheses to be used to represent a symbol:

.demo
    x -> y -- -> z
    ---

    &
        (->) = &rarr;
        (--) = &mdash;
        (---) = <hr>

The symbol notations are simply nicer to read than the standard HTML entity notation.

As another example, the typewriter apostrophe may be used to directly represent a typographical apostrophe. The same character is used for single quote marks, but single quotes can be defined with a bracket label, which allows apostrophe characters to be used inside single quotes:

.demo
    That's '[Joes's thing].

    &
        (')  = &rsquo;
        '    = squo
        squo :: (content) => '&lsquo;'+markit('myword',content)+'&rsquo;'

It is quite rare to need a plain typewriter apostrophe, except in code or keyboard text, which is no problem using code quotes:

.demo
    It's `p's and q's`

But if required you can always redefine the apostrophe symbol to be a plain typewriter apostrophe:

.demo
    It's p's and q's

    &  (') = &apos;

A symbol may be written inside parentheses, just as they are defined. This allows symbols to be named with any text:

.demo
    (c) (tm) (->) (+1) (smiley)

    &
        (c) = &copy;
        (tm) = &trade;
        (->) = &rarr;
        (+1) = &#x1F44D;
        (smiley) = &#x263A;

The `->` is the only symbol in this example that could be used without brackets.

Symbols can be defined in the same way as any other label, with a transform and a HTML wrapper:

.demo
    All heart (heart), the date is: (date).

    &
        (heart) = &hearts; <span style='color:red'>

        (date) = dateNow <b>
        dateNow :: () => new Date().toDateString()

Symbols written inside brackets can be thought of as a special case of bracket elements with an empty label.


##  Quotes

HTML provides a <q> tag that can be used for typographical quotation marks:

.demo
    Nice :q[typographical] quote marks.

    &  :q = <q>

Even better, quotes can be defined directly as a notation. They are defined as a pattern using quote marks around empty brackets:

.demo
    Nice "typographical" quote marks.

    &  "()" = <q>

The are five quote mark characters `[` " * _ ~]  that may be used to define quote notations. These quote marks allow the familiar Markdown notations to be emulated:

.demo
    `if (a>b) {...}`

    *for emphasis*

    __for strong emphasis__

    ~~strike this out~~

    &
        `()` = text <code>
        *()* = <em>
        __()__ = <strong>
        ~~()~~ = <s>

Different quotes can be defined using any of the quote mark characters, and with any number of the same quote mark. Different quotes can be nested:

.demo
    *some **bold** in italics*

    **bold with some *italics* inside**

    &
        *()*   = <i>
        **()** = <b>

Quote notations are nice and easy to read, but in MyWord they are quite restricted. Quotes can never contain their own end quote mark. The first quote mark that matches the start quote mark will immediately end the quotes. This is not always what you want. Also, quotes (other than code quotes) can only start at a word boundary, that is, they can not start inside a word. Nested quotes with multiple quote marks can not be merged together. Markdown covers lots of these special cases, but it is quite hard to understand. The MyWord quotes are restrictive, but they are simple and easy to understand.

The MyWord bracket notations have no such restrictions:

.demo
    *[including * characters], but *not without * brackets*.

    A long*[and**[bold]] word, but not long*and**bold***.

    &
        *[]    = <i>
        **[]   = <b>

        *()*   = <i>
        **()** = <b>

Quote notations are simple and easy to read, but they can not handle complicated expressions. Just as in arithmetic, when things get more complicated then you need to use brackets.


##  Double Bracket Notations

A MyWord inline label may be defined with a second set of brackets like this:

.demo
    For example, @[link to something](http://example.com)

    &
        @[]()   = linkURL
        linkURL :: (txt,url) => "<a href='"+url+"'>"+txt+"</a>"

An image can be defined in much the same way:

.demo
    This image: ![a gopher](images/gopher.png)

    &
        ![]()    = imageURL
        imageURL :: (txt,url) => "<img src='"+url+"' title='"+txt+"' />"

This emulates the Markdown image notation.

A `[[]()] pattern without a label can be defined to match double brackets, and the same linkURL transform can be used as before.

.demo
    For example, [link to something](http://example.com)

    &
        []()    = linkURL
        linkURL :: (txt,url) => "<a href='"+url+"'>"+txt+"</a>"

These double brackets act as if they have an implicit @ label.

The Markdown link reference definitions can also be emulated by using a block label that matches the Markdown `[[]:] format:

.demo
    For example, a [link to something][eg]

    [eg]:  http://example.com

    &
        [][]    = linkURL
        linkURL :: (txt,url) => "<a href='"+url+"'>"+txt+"</a>"

The reference definition `[[eg]:] is a special block label that is used to define the contents of the second brackets.


##  Comma Labels

A block label may contain any characters except for white-space. An inline label has a few more restrictions, it may not contain bracket characters, and a comma has a special meaning. A comma can be used to split a label into two parts. The text before the comma is simply part of the preceding text, the text after the comma becomes the label.

A comma should only be needed in rare situations, so the examples are rather contrived, but they illustrates what happens:

.demo
    **,*[this thing]

    &
        *[]    = <i>
        **[]   = <b>
        ***[]  = <mark>

A comma immediately before an opening bracket becomes an empty label. This allows a symbol in brackets to be used without the need for white-space:

.demo
    --,(->)

    &
        --()   = <mark>
        (--)   = &mdash;
        (->)   = &rarr;


##  Markdown Compatibility

MyWord is designed to allow any syntax to be used in the content of a block label, or the content of a bracket label. This means that a standard Markdown parser can be used to translate prose paragraphs. But there is no way to extend the Markdown language, all the notations are pre-defined. The only way for an author to extend Markdown is to use standard HTML.

Many users only need a simple basic version of Markdown, something which is very easy to read and understand, which was the intent for Markdown in the first place. Beyond the basics Markdown the details get very complicated, see the [Common Mark Specification](http://commonmark.org/).  Since many authors only need a simple form of Markdown an alternative is to use MyWord with a lingo that emulates the Markdown notations. For anything more complicated the MyWord notations can be used in combination with the basic Markdown notations. Many simple Markdown documents may be used in MyWord without any changes.

In Markdown each notation has a fixed semantic meaning, so that different quote marks can have very different rules. For example, the star quotes always represent emphasis, and there are special rules for different kinds of quotes. In MyWord the quote notations are very simple, and they all work exactly the same way (other than the priority given to code quotes). This works well for many documents but the MyWord bracket notations may be needed for more complex expressions. For example when nested quotes overlap. The MyWord brackets can cope with these situations in much the same way that brackets are used in arithmetic. Markdown has no equivalent to the MyWord bracket notation.

In MyWord all the block labels, such as a `[##] header label, or a `[>] blockquote label, must be offset with a tab or two or more spaces (Markdown allows a single space). This offset format ensures that a label can always be distinguished from the same text being used as the first word in a line of normal prose. In MyWord the user can choose to define any word as a block label. Also, if the content of a block label extends over multiple lines then in MyWord these lines must be indented under the label to show that structure.

In MyWord any number of labels can be defined, so it is usually best for each label to have a single meaning. In Markdown the notations are restricted to a small set of symbols, and the same symbol can have more than one meaning. For example, the * is used as a bullet point to label a list element, but it is also used as a quote mark for `[*emphasis*]. These notations may be emulated in MyWord like this:

.demo
    *  A bullet point with some *emphasis*.

    *  Another point with *[italic text].

    &
        *    = list <ul>
        *[]  = <i>
        *()* = <em>

A label definition without any brackets defines a block label. A label can be defined with either parentheses or square brackets, the defintion applies to both so that the author is free to choose either type of brackets. The empty brackets in the quote pattern can be either type. If symbol definitions are used with brackets then these must match their definition.

In MyWord there are no predefined built-in definitions, so to use Markdown notations requires a MyWord lingo to define the Markdown notations.


##  Label Syntax

This is just a sandbox to play around with experimental label syntax.

If the only restrictions are not brackets, and no commas, then this should work:

.demo
    Some i[italic] and b[bold] words.

    &
        i[] = <i>
        b[] = <b>

How about functions in prose:

.demo
    The .js[f(x)] function ...

    A ~ f(x)~ function ...

    &
        .js[] = text <i>
        ~()~  = text <i>

The space is used to prevent the ~f from being seen as an undefined label, but extra space is no problem in HTML. But I also had to relax the quote start rule, it was very restrictive, and required an alphanumeric after the start quote mark. Perhaps this is ok given there are only five quote marks?

But maybe a better idea is to restrict the label syntax and not allow a quote symbol? It turns out that this can be implemented very simply by failing to match a label that starts with a quote mark. So there is no need to restrict the label syntax at all! If a label is not defined, then before flagging the undefined label check if it starts with a quote mark and if so then fail the elem and try for a quoteStart:

.demo
    The .js[f(x)] function ...

    A ~f(x)~ function, or "f(x)" or **f(x)** ...

    &
        .js[] = text <i>
        ~()~  = text <i>
        "()"  = text <q>
        **()**  = text <b>

So now a label bracket element can be quoted, simply with the convention that labels should not begin with a quote mark.

But no problem if a quote mark label is defined however, that should work too:

.demo
    A _foo(bar) element and _some thing_ should both work...

    &
        _()_    = <i>
        _foo()  = <i>




&  " = dquo
   dquo :: (c) => '&ldquo;'+markit('myword',c)+'&rdquo;'

&  ' = squo
   squo :: (c) => '&lsquo;'+markit('myword',c)+'&rsquo;'

&  []() = link2url
    link2url :: (args) => {
        return "<a href='"+args.split('\t')[1]+"'>"+args.split('\t')[0]+"</a>"; }

&  [][] = link2ref
    link2ref :: (args) => {
        var ref = args.split('\t')[1]
        var url = markit.metadata('['+ref+']:');
        if (!url) return "["+args.split('\t')[0]+"][<mark>"+ref+"</mark>]";
        return "<a href='"+url+"'>"+args.split('\t')[0]+"</a>"; }
